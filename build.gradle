//I am new to gradle, if anything is wrong or could be improved please tell me <3
plugins {
    id 'java'
    id 'idea'
    id 'ninja.miserable.blossom' version '1.0.1'
    id 'com.github.johnrengelman.shadow' version '1.2.4'
    id 'org.spongepowered.plugin' version '0.8.1'
}

//Project details
description "All you need to set up a server and more!"
group "bammerbom"
version "3.0.8-SNAPSHOT"

sourceCompatibility = 1.8
targetCompatibility = 1.8

jar {
    manifest.attributes("Main-Class": "bammerbom.ultimatecore.Main")
    archiveName = "UltimateCore.jar"
}

blossom {
    replaceToken '@VERSION@', project.version, 'src/main/java/bammerbom/ultimatecore/sponge/UltimateCore.java'
}

//Dependencies
repositories {
    mavenCentral()
    maven {
        name 'Sponge repo'
        url 'http://repo.spongepowered.org/maven'
    }
    maven {
        name 'bstats'
        url = 'https://repo.bstats.org/content/groups/public/'
    }
    maven {
        name = 'minecrell'
        url = 'http://repo.minecrell.net/releases'
    }
    maven {
        name = "Nuvotifier"
        url = "https://dl.bintray.com/nuvotifier/maven/"
    }
    maven {
        name = "Nucleus"
        url = "http://repo.drnaylor.co.uk/artifactory/list/minecraft"
    }
}

dependencies {
    //Don't shadow
    compile "org.spongepowered:spongeapi:6.0.0-SNAPSHOT"
    compile "com.vexsoftware:nuvotifier-universal:2.3.4"
    compile "io.github.nucleuspowered:Nucleus:0.25.0-S6.0"

    //Shadow
    compile "com.goebl:david-webb:1.3.0"
    compile 'org.bstats:bstats-sponge:1.1'
    compile 'net.minecrell:statusprotocol:0.3'

    //Geoip stuff
    compile 'com.maxmind.geoip2:geoip2:2.8.1'
    compile 'com.maxmind.db:maxmind-db:1.2.2'
    compile 'com.fasterxml.jackson.core:jackson-core'
    compile 'com.fasterxml.jackson.core:jackson-databind'
    compile 'com.fasterxml.jackson.core:jackson-annotations'
}

shadowJar {
    archiveName = "UltimateCore.jar"
    classifier = null
    version = null
    dependencies {
        include(dependency("com.goebl:david-webb:1.3.0"))
        include(dependency('org.bstats:bstats-sponge:1.1'))
        include(dependency('net.minecrell:statusprotocol:0.3'))

        //Geoip stuff
        include(dependency("com.maxmind.geoip2:geoip2:2.8.1"))
        include(dependency("com.maxmind.db:maxmind-db:1.2.2"))
        include(dependency("com.fasterxml.jackson.core:jackson-core"))
        include(dependency("com.fasterxml.jackson.core:jackson-databind"))
        include(dependency("com.fasterxml.jackson.core:jackson-annotations"))
    }

    //Relocate
    relocate 'com.goebl.david', 'dependencies.bammerbom.ultimatecore.com.goebl.david'
    relocate 'org.bstats', 'dependencies.bammerbom.ultimatecore.org.bstats'
    relocate 'net.minecrell.statusprotocol', 'dependencies.bammerbom.ultimatecore.net.minecrell.statusprotocol'

    relocate 'com.fasterxml.jackson', 'dependencies.bammerbom.ultimatecore.com.fasterxml.jackson'
    relocate 'com.maxmind.geoip2', 'dependencies.bammerbom.ultimatecore.com.maxmind.geoip2'
    relocate 'com.maxmind.db', 'dependencies.bammerbom.ultimatecore.com.maxmind.db'
}
build.dependsOn(shadowJar)